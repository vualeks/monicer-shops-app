apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

static def generateVersionCode() {
    def result = "git rev-list --all --count".execute().text.trim()
    if (result.empty) result = "PowerShell -Command git rev-list HEAD --count".execute().text.trim()
    if (result.empty) result = 1
    return result.toInteger()
}

def majorVersion = "1.0"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "me.hackathon.monicershopsapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode generateVersionCode()
        versionName "${majorVersion}.${generateVersionCode()}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Monicer Shops"
            resValue "string", "test_username", ""
            resValue "string", "test_password", ""
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Monicer Shops"
            resValue "string", "test_username", ""
            resValue "string", "test_password", ""
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'

    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Architecture Components
    implementation "androidx.lifecycle:lifecycle-livedata:$arch_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$arch_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$arch_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$arch_version"
    implementation "androidx.room:room-runtime:$arch_version"
    kapt "androidx.room:room-compiler:$arch_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    //noinspection GradleDependency
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Support library
    implementation "androidx.appcompat:appcompat:$support_version"
    implementation "com.google.android.material:material:$support_version"
    implementation "androidx.cardview:cardview:$support_version"

    // Constraint layout
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayout_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Lottie
    implementation "com.airbnb.android:lottie:$lottie_version"
}